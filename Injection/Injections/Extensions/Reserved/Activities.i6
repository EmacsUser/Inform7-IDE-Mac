! Activities.i6 segment

Global activities_sp = 0;
Array activities_stack --> 25;
Array activities_ostack --> 25;
Global inhibit_flag = 0;
Global inhibit_dr = 0;
[ FixInhibitFlag n i_f;
	for (n=0:n<activities_sp:n++)
		if (activities_stack-->n <= 4) i_f = true;
	if ((inhibit_flag == false) && (i_f)) {
		inhibit_dr = debug_rules;
		debug_rules = 0;
	}
	if ((inhibit_flag) && (i_f == false)) {
		debug_rules = inhibit_dr;
	}
	inhibit_flag = i_f;
];
[ BeginActivity A o x;
!	print "Begin ", A, "^";
	if (activities_sp == 25) return RunTimeProblem(RTP_TOOMANYACTS);
	activities_ostack-->activities_sp = o;
	activities_stack-->(activities_sp++) = A;
	FixInhibitFlag();
	if (Activity_atb_rulebooks->A) { x = action; action = action_to_be; }
	o = FollowRulebookSelfed(Activity_before_rulebooks-->A, o);
	if (Activity_atb_rulebooks->A) action = x;
	return o;
];
[ ForActivity A o x;
!	print "For ", A, "^";
	if (Activity_atb_rulebooks->A) { x = action; action = action_to_be; }
	o = FollowRulebookSelfed(Activity_when_rulebooks-->A, o);
	if (Activity_atb_rulebooks->A) action = x;
	return o;
];
[ EndActivity A o rv x;
!	print "End ", A, "^";
	if (activities_sp == 0) jump BadEnd;
	if (activities_stack-->(activities_sp-1) == A) {
		if (Activity_atb_rulebooks->A) { x = action; action = action_to_be; }
		rv = FollowRulebookSelfed(Activity_after_rulebooks-->A, o);
		if (Activity_atb_rulebooks->A) action = x;
		activities_sp--; FixInhibitFlag();
!		print "Ended^";
		return rv;
	}
	.BadEnd; return RunTimeProblem(RTP_CANTABANDON);
];
[ AbandonActivity A o;
	if (activities_sp == 0) jump BadEnd;
	if (activities_stack-->(activities_sp-1) == A) {
		activities_sp--; FixInhibitFlag(); return; }
	.BadEnd; return RunTimeProblem(RTP_CANTEND);
];
[ CarryOutActivity A o rv;
	BeginActivity(A,o); rv = ForActivity(A,o); EndActivity(A,o); return rv; ];
[ TestActivity A Pattern i;
	for (i=0:i<activities_sp:i++)
		if (activities_stack-->i == A) {
			if (Pattern == false) rtrue;
			if ((Pattern)(activities_ostack-->i)) rtrue;
		}
	rfalse;
];
[ I7_Amusing_Provided x;
	x = Activity_when_rulebooks-->AMUSING_ACT;
	if (((rulebooks_array-->x)-->0) == I6_NULL)
		rfalse;
	rtrue;
];

! End of Activities.i6
